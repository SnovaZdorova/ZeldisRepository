ВЫБРАТЬ
	ВТ_Комплектующие.ТипКомпонента,
	ВЫРАЗИТЬ(ВТ_Комплектующие.Компонент КАК Справочник.зфНИОКР_Сырье) КАК Компонент,
	ВТ_Комплектующие.Поставщик,
	ВТ_Комплектующие.Процент,
	ВТ_Комплектующие.ЕдИзм,
	ВТ_Комплектующие.Норматив,
	ВТ_Комплектующие.НормативПроцент,
	ВТ_Комплектующие.НормативРасход,
	ВТ_Комплектующие.ЦенаСНДС,
	ВТ_Комплектующие.СтоимостьНаЕд,
	ВТ_Комплектующие.РазницаЦен,
	ВТ_Комплектующие.ЦенаВВалюте,
	ВТ_Комплектующие.ВалютаКонтракта,
	ВТ_Комплектующие.бВыбор,
	ВТ_Комплектующие.ЦенаПоКурсу
ПОМЕСТИТЬ ВТ_Комплектующие
ИЗ
	&Комплектующие КАК ВТ_Комплектующие
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	МАКСИМУМ(ЗакупкиОбороты.ДокументЗакупки.Дата) КАК МаксДата,
	ЗакупкиОбороты.Номенклатура,
	ЗакупкиОбороты.Контрагент
ПОМЕСТИТЬ ВТ_ДатыПоступления
ИЗ
	РегистрНакопления.Закупки.Обороты(
			&ГодНазад,
			&ТекущаяДата,
			Период,
			Номенклатура В
				(ВЫБРАТЬ
					ТК.Компонент.Номенклатура
				ИЗ
					ВТ_Комплектующие КАК ТК)) КАК ЗакупкиОбороты

СГРУППИРОВАТЬ ПО
	ЗакупкиОбороты.Номенклатура,
	ЗакупкиОбороты.Контрагент
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	СУММА(ЗакупкиОбороты.СтоимостьОборот / ЗакупкиОбороты.КоличествоОборот) КАК СтоимостьОборот,
	ЗакупкиОбороты.Номенклатура,
	ЗакупкиОбороты.ДокументЗакупки,
	ЗакупкиОбороты.Контрагент
ПОМЕСТИТЬ ВТ_Закупки
ИЗ
	РегистрНакопления.Закупки.Обороты(
			&ГодНазад,
			&ТекущаяДата,
			Период,
			Номенклатура В
				(ВЫБРАТЬ
					ТК.Компонент.Номенклатура
				ИЗ
					ВТ_Комплектующие КАК ТК)) КАК ЗакупкиОбороты
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДатыПоступления КАК ВТ_ДатыПоступления
		ПО ЗакупкиОбороты.Контрагент = ВТ_ДатыПоступления.Контрагент
			И ЗакупкиОбороты.Номенклатура = ВТ_ДатыПоступления.Номенклатура
			И ЗакупкиОбороты.ДокументЗакупки.Дата = ВТ_ДатыПоступления.МаксДата

СГРУППИРОВАТЬ ПО
	ЗакупкиОбороты.Контрагент,
	ЗакупкиОбороты.Номенклатура,
	ЗакупкиОбороты.ДокументЗакупки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВложенныйЗапрос.ТипКомпонента,
	ВложенныйЗапрос.Компонент,
	ВложенныйЗапрос.Поставщик,
	МАКСИМУМ(ВложенныйЗапрос.Процент) КАК Процент,
	ВложенныйЗапрос.ЕдИзм,
	МАКСИМУМ(ВложенныйЗапрос.Норматив) КАК Норматив,
	ВложенныйЗапрос.НормативПроцент,
	МАКСИМУМ(ВложенныйЗапрос.НормативРасход) КАК НормативРасход,
	СУММА(ВложенныйЗапрос.ЦенаСНДС) КАК ЦенаСНДС,
	СУММА(ВложенныйЗапрос.СтоимостьНаЕд) КАК СтоимостьНаЕд,
	СУММА(ВложенныйЗапрос.РазницаЦен) КАК РазницаЦен,
	СУММА(ВложенныйЗапрос.ЦенаВВалюте) КАК ЦенаВВалюте,
	МАКСИМУМ(ВложенныйЗапрос.ВалютаКонтракта) КАК ВалютаКонтракта,
	ВложенныйЗапрос.бВыбор,
	ВложенныйЗапрос.Номенклатура
ПОМЕСТИТЬ ВТ_Компоненты_Закупка
ИЗ
	(ВЫБРАТЬ
		ВТ_Комплектующие.ТипКомпонента КАК ТипКомпонента,
		ВТ_Комплектующие.Компонент КАК Компонент,
		ЕСТЬNULL(ВТ_Закупки.Контрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Поставщик,
		ВТ_Комплектующие.Процент КАК Процент,
		ВТ_Комплектующие.ЕдИзм КАК ЕдИзм,
		ВТ_Комплектующие.Норматив КАК Норматив,
		ВТ_Комплектующие.НормативПроцент КАК НормативПроцент,
		ВТ_Комплектующие.НормативРасход КАК НормативРасход,
		ЕСТЬNULL(ВТ_Закупки.СтоимостьОборот, 0) КАК ЦенаСНДС,
		ВТ_Комплектующие.СтоимостьНаЕд КАК СтоимостьНаЕд,
		ВТ_Комплектующие.РазницаЦен КАК РазницаЦен,
		ВТ_Комплектующие.ЦенаВВалюте КАК ЦенаВВалюте,
		ВТ_Комплектующие.ВалютаКонтракта КАК ВалютаКонтракта,
		ВТ_Комплектующие.бВыбор КАК бВыбор,
		ЕСТЬNULL(ВТ_Закупки.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура
	ИЗ
		ВТ_Комплектующие КАК ВТ_Комплектующие
			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Закупки КАК ВТ_Закупки
			ПО ВТ_Комплектующие.Компонент.Номенклатура = ВТ_Закупки.Номенклатура
	
	ОБЪЕДИНИТЬ ВСЕ
	
	ВЫБРАТЬ
		ВТ_Комплектующие.ТипКомпонента,
		ВТ_Комплектующие.Компонент,
		ЕСТЬNULL(зфНИОКР_СырьеПоставщики.Поставщик, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
		ВТ_Комплектующие.Процент,
		ВТ_Комплектующие.ЕдИзм,
		ВТ_Комплектующие.Норматив,
		ВТ_Комплектующие.НормативПроцент,
		ВТ_Комплектующие.НормативРасход,
		0,
		ВТ_Комплектующие.СтоимостьНаЕд,
		ВТ_Комплектующие.РазницаЦен,
		ЕСТЬNULL(зфНИОКР_СырьеПоставщики.Цена, 0),
		ЕСТЬNULL(зфНИОКР_СырьеПоставщики.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)),
		ВТ_Комплектующие.бВыбор,
		ВТ_Комплектующие.Компонент.Номенклатура
	ИЗ
		ВТ_Комплектующие КАК ВТ_Комплектующие
			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.зфНИОКР_Сырье.Поставщики КАК зфНИОКР_СырьеПоставщики
			ПО ВТ_Комплектующие.Компонент = зфНИОКР_СырьеПоставщики.Ссылка) КАК ВложенныйЗапрос

СГРУППИРОВАТЬ ПО
	ВложенныйЗапрос.Компонент,
	ВложенныйЗапрос.ТипКомпонента,
	ВложенныйЗапрос.НормативПроцент,
	ВложенныйЗапрос.Поставщик,
	ВложенныйЗапрос.ЕдИзм,
	ВложенныйЗапрос.Номенклатура,
	ВложенныйЗапрос.бВыбор
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_Компоненты_Закупка.ТипКомпонента,
	ВТ_Компоненты_Закупка.Компонент,
	ВТ_Компоненты_Закупка.Поставщик,
	ВТ_Компоненты_Закупка.ЕдИзм,
	ЕСТЬNULL(ВТ_Компоненты_Закупка.Процент, 0) КАК Процент,
	ЕСТЬNULL(ВТ_Компоненты_Закупка.Норматив, 0) КАК Норматив,
	ЕСТЬNULL(ВТ_Компоненты_Закупка.НормативПроцент, 0) КАК НормативПроцент,
	ЕСТЬNULL(ВТ_Компоненты_Закупка.НормативРасход, 0) КАК НормативРасход,
	ЕСТЬNULL(ВТ_Компоненты_Закупка.ЦенаСНДС, 0) КАК ЦенаСНДС,
	ЕСТЬNULL(ВТ_Компоненты_Закупка.ЦенаСНДС * ВТ_Компоненты_Закупка.НормативРасход, 0) КАК СтоимостьНаЕд,
	ВЫРАЗИТЬ(ВТ_Компоненты_Закупка.ЦенаСНДС - ВТ_Компоненты_Закупка.ЦенаВВалюте * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) / ВЫБОР
			КОГДА КурсыВалютСрезПоследних.Кратность = 0
				ТОГДА 1
			ИНАЧЕ КурсыВалютСрезПоследних.Кратность
		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК РазницаЦен,
	ВЫРАЗИТЬ(ВТ_Компоненты_Закупка.ЦенаВВалюте КАК ЧИСЛО(15, 2)) КАК ЦенаВВалюте,
	ВТ_Компоненты_Закупка.бВыбор,
	ВЫРАЗИТЬ(ВТ_Компоненты_Закупка.ЦенаВВалюте * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) / ВЫБОР
			КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) = 0
				ТОГДА 1
			ИНАЧЕ ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1)
		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ЦенаПоКурсу,
	ВТ_Компоненты_Закупка.Номенклатура,
	ВТ_Компоненты_Закупка.ВалютаКонтракта
ПОМЕСТИТЬ ВТ_КонтрактныеПоКонтрагенту
ИЗ
	ВТ_Компоненты_Закупка КАК ВТ_Компоненты_Закупка
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ТекущаяДата, ) КАК КурсыВалютСрезПоследних
		ПО ВТ_Компоненты_Закупка.ВалютаКонтракта = КурсыВалютСрезПоследних.Валюта
;

////////////////////////////////////////////////////////////////////////////////
УНИЧТОЖИТЬ ВТ_ДатыПоступления
;

////////////////////////////////////////////////////////////////////////////////
УНИЧТОЖИТЬ ВТ_Закупки
;

////////////////////////////////////////////////////////////////////////////////
УНИЧТОЖИТЬ ВТ_Компоненты_Закупка
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_КонтрактныеПоКонтрагенту.ТипКомпонента КАК ТипКомпонента,
	ВТ_КонтрактныеПоКонтрагенту.Компонент КАК Компонент,
	ВТ_КонтрактныеПоКонтрагенту.Поставщик КАК Поставщик,
	ВТ_КонтрактныеПоКонтрагенту.ЕдИзм КАК ЕдИзм,
	СУММА(ВТ_КонтрактныеПоКонтрагенту.СтоимостьНаЕд) КАК СтоимостьНаЕд,
	СУММА(ВТ_КонтрактныеПоКонтрагенту.РазницаЦен) КАК РазницаЦен,
	СУММА(ВТ_КонтрактныеПоКонтрагенту.ЦенаВВалюте) КАК ЦенаВВалюте,
	ВТ_КонтрактныеПоКонтрагенту.бВыбор КАК бВыбор,
	СУММА(ВТ_КонтрактныеПоКонтрагенту.ЦенаПоКурсу) КАК ЦенаПоКурсу,
	ВТ_КонтрактныеПоКонтрагенту.Процент КАК Процент,
	МАКСИМУМ(ВТ_КонтрактныеПоКонтрагенту.Норматив) КАК Норматив,
	МАКСИМУМ(ВТ_КонтрактныеПоКонтрагенту.НормативПроцент) КАК НормативПроцент,
	МАКСИМУМ(ВТ_КонтрактныеПоКонтрагенту.НормативРасход) КАК НормативРасход,
	СУММА(ВТ_КонтрактныеПоКонтрагенту.ЦенаСНДС) КАК ЦенаСНДС,
	СУММА(ВТ_КонтрактныеПоКонтрагенту.ЦенаПоКурсу * ВТ_КонтрактныеПоКонтрагенту.НормативРасход) КАК СтоимостьНаЕдКонтр,
	СУММА(ВТ_КонтрактныеПоКонтрагенту.ЦенаВВалюте * ВТ_КонтрактныеПоКонтрагенту.НормативРасход) КАК СтоимостьНаЕдВал,
	МАКСИМУМ(ВТ_КонтрактныеПоКонтрагенту.ВалютаКонтракта) КАК ВалютаКонтракта
ИЗ
	ВТ_КонтрактныеПоКонтрагенту КАК ВТ_КонтрактныеПоКонтрагенту

СГРУППИРОВАТЬ ПО
	ВТ_КонтрактныеПоКонтрагенту.ТипКомпонента,
	ВТ_КонтрактныеПоКонтрагенту.Компонент,
	ВТ_КонтрактныеПоКонтрагенту.Поставщик,
	ВТ_КонтрактныеПоКонтрагенту.ЕдИзм,
	ВТ_КонтрактныеПоКонтрагенту.бВыбор,
	ВТ_КонтрактныеПоКонтрагенту.Процент
ИТОГИ
	МАКСИМУМ(ТипКомпонента),
	МАКСИМУМ(Поставщик),
	МАКСИМУМ(ЕдИзм),
	СРЕДНЕЕ(СтоимостьНаЕд),
	МАКСИМУМ(РазницаЦен),
	СРЕДНЕЕ(ЦенаВВалюте),
	МАКСИМУМ(бВыбор),
	СРЕДНЕЕ(ЦенаПоКурсу),
	МАКСИМУМ(Процент),
	МАКСИМУМ(Норматив),
	МАКСИМУМ(НормативПроцент),
	МАКСИМУМ(НормативРасход),
	СРЕДНЕЕ(ЦенаСНДС),
	СРЕДНЕЕ(СтоимостьНаЕдКонтр),
	СРЕДНЕЕ(СтоимостьНаЕдВал),
	МАКСИМУМ(ВалютаКонтракта)
ПО
	Компонент
;

////////////////////////////////////////////////////////////////////////////////
УНИЧТОЖИТЬ ВТ_Комплектующие
;

////////////////////////////////////////////////////////////////////////////////
УНИЧТОЖИТЬ ВТ_КонтрактныеПоКонтрагенту